plugins {
    id 'org.cadixdev.licenser' version '0.6.1' apply true
}

def determinePatchVersion() {
    // get the name of the last tag
    def tagInfo = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = tagInfo
    }
    tagInfo = tagInfo.toString()
    if (!tagInfo.contains('-')) {
        return 0
    }
    return tagInfo.split("-")[1]
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.cadixdev.licenser'

    group = 'me.pietelite.nope'

    project.ext.publishApi = false
    project.ext.majorVersion = '2'
    project.ext.minorVersion = '0'
    project.ext.patchVersion = determinePatchVersion()
    project.ext.versionPrefix = 'BETA-'
    project.ext.apiVersion = project.ext.versionPrefix + project.ext.majorVersion + '.' + project.ext.minorVersion
    project.ext.fullVersion = project.ext.apiVersion + '.' + project.ext.patchVersion
    version = project.ext.fullVersion

    sourceCompatibility = JavaVersion.toVersion(11)
    targetCompatibility = JavaVersion.toVersion(11)

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    jar {
        from '../LICENSE.txt'
    }

    license {
        header = rootProject.file('HEADER.txt')
        include 'src/**/*.java'
        newLine = true
    }

    repositories {
        mavenCentral()
    }

}
