plugins {
    id 'java-library'
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.spongepowered.org/repository/maven-public'
    }
}

dependencies {
    implementation project(':common-api')

    implementation "org.spongepowered:spongeapi:8.0.0"
    annotationProcessor "org.spongepowered:spongeapi:8.0.0"

    implementation 'org.jetbrains:annotations:23.0.0'
}

jar {
    archiveFileName = "Nope-SpongeAPI-${project.ext.apiVersion}.jar"
}

// Used for deployment of the Sponge API
if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword') && project.ext.publishApi && false) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    javadoc {
        title = 'Nope Sponge API (v' + project.ext.apiVersion + ')'
        options.encoding = 'UTF-8'
        options.charSet = 'UTF-8'
        options.links(
                'https://javadoc.io/doc/org.jetbrains/annotations/23.0.0'
        )
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
        from javadoc.destinationDir
    }
    task sourcesJar(type: Jar) {
        classifier 'sources'
        from sourceSets.main.allSource
    }
    artifacts {
        archives javadocJar
        archives sourcesJar
    }
    publishing {
        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                groupId = 'me.pietelite.nope'
                artifactId = 'sponge-api'
                version = project.ext.apiVersion
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = 'Nope Sponge API'
                    description = 'A world-management plugin for Minecraft servers.'
                    url = 'https://github.com/pietelite/nope'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'pietelite'
                            name = 'PietElite'
                            url = 'https://pietelite.me'
                            email = 'pietelite@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/pietelite/nope.git'
                        developerConnection = 'scm:git:git@github.com:pietelite/nope.git'
                        url = 'https://github.com/pietelite/nope'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/pietelite/nope/issues'
                    }
                }
            }
        }

        signing {
            useGpgCmd()
            sign configurations.archives
            sign publishing.publications.mavenJava
            required = true
        }
    }
}
