import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
    id 'java-library'
    id 'org.spongepowered.gradle.plugin' version '2.0.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenCentral()
}

sponge {
    apiVersion("8.0.0")
    license("LICENSE.txt")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version(project.ext.fullVersion)
    }
    plugin("nope") {
        displayName("Nope")
        entrypoint("me.pietelite.nope.sponge.SpongeNope")
        description("A powerful protections plugin")
        links {
            homepage("https://github.com/pietelite")
            source("https://github.com/pietelite/nope")
            issues("https://github.com/pietelite/nope/issues")
        }
        contributor("PietElite") {
            description("Author")
        }
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
    }
}

java {
    sourceCompatibility = JavaVersion.toVersion(8)
    targetCompatibility = JavaVersion.toVersion(8)
}

tasks.withType(JavaCompile.class).configureEach {
    options.encoding = "utf-8"
}

// Make sure all tasks which produce archives (jar, sources jar, javadoc jar, etc) produce more consistent output
tasks.withType(AbstractArchiveTask.class).configureEach {
    reproducibleFileOrder = true
    preserveFileTimestamps = false
}
//////////////////////////

test {
    useJUnitPlatform()
}

dependencies {
    implementation project(':common')
    implementation project(':common-api')
    implementation project(':sponge-api')

    implementation 'org.spongepowered:spongeapi:8.0.0'
    annotationProcessor "org.spongepowered:spongeapi:8.0.0"

    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.jetbrains:annotations:23.0.0'

    implementation 'org.bstats:bstats-sponge:3.0.0'
}

jar.enabled = false

shadowJar {
    archiveFileName = "Nope-Sponge-${project.ext.fullVersion}.jar"

    dependencies {
        include(dependency('me.pietelite.nope:.*'))
        include(dependency('org.bstats:.*'))
    }

    relocate 'org.bstats', 'me.pietelite.nope.lib.bstats'
}

build {
    finalizedBy "shadowJar"
}
